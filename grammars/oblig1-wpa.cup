package oblig1parser;
import java_cup.runtime.*;
import syntaxtree.*;
import java.util.*;

parser code {:

:};

/* Terminals */
terminal                        CLASS; 
terminal                        REFERENCE;
terminal                        PROCEDURE;
terminal                        VARIABLE;
terminal	                ASSIGNMENT;

terminal String                 FLOAT, INT, STRING, BOOL;
terminal 			                  LBRACK, RBRACK;
terminal                        LCHEV, LCHEVEQ, RCHEV, RCHEVEQ;
terminal                        EQUALS, NEQUAL;
terminal                        AND, OR, NOT;
terminal                        TRUE, FALSE;
terminal                        NULL;
terminal                        DOT;
terminal                        NEW;
terminal                        PLUS, MINUS, TIMES, DIVIDE, EXPO;
terminal 			                  LPAR, RPAR;
terminal                        SEMI, RETURN;
terminal			                  IF, THEN, ELSE, WHILE, DO;
terminal			                  RET;
terminal			                  COMMA;

terminal String                 ID;
terminal String                 STRING_LITERAL;
terminal String                 INT_LITERAL;
terminal String                 FLOAT_LITERAL;

/* Non terminals */
non terminal Program	          program;
non terminal Decl               decl;
non terminal List<Decl>         decl_list;
non terminal VarDecl            var_decl;
non terminal List<Decl>         var_list;
non terminal ProcDecl           proc_decl; 
non terminal String             proc_decl_type;
non terminal List<Decl>         proc_decl_decls;
non terminal ClassDecl          class_decl;
non terminal ParamDecl          param_decl;
non terminal List<Decl>         param_list;
non terminal Boolean            param_ref;

non terminal Exp                exp;
non terminal Exp                var;
non terminal String             log_op;
non terminal String             rel_op;
non terminal String             arit_op1;
non terminal String		arit_op2;
non terminal String             literal;
non terminal Exp                actual_param;
non terminal String             type;


non terminal Stmt stmt;
non terminal List<Stmt> stmts;
non terminal List<Stmt> stmt_list;

non terminal Stmt assign_stmt;
non terminal Stmt if_stmt;
non terminal List<Stmt> if_else;
non terminal Stmt while_stmt;
non terminal Stmt return_stmt;
non terminal CallStmt call_stmt;
non terminal List<Exp> call_params;
non terminal Exp return_val;
non terminal Exp or_exp;
non terminal Exp and_exp;
non terminal Exp not_exp;
non terminal Exp rel_exp;
non terminal Exp arit1_exp;
non terminal Exp arit2_exp;
non terminal Exp expo_exp;
non terminal Exp last_exp;


/* The grammar */
program			            ::= decl_list:dl
                          {: RESULT = new Program(dl); :} 
				                ;

decl_list               ::= decl:d {: List<Decl> l = new LinkedList<Decl>();
                            l.add(d);
                            RESULT = l; :} 
                          | decl_list:dl decl:d 
                            {: dl.add(d); RESULT = dl; :}
                        ;

decl                    ::= class_decl:sd {: RESULT = sd; :}
                          | proc_decl:pd {:RESULT = pd; :} 
                          | var_decl:vd {: RESULT = vd; :}
                        ;
                        

var_decl                ::= VARIABLE type:t ID:name SEMI
                          {: RESULT = new VarDecl(name, t); :} 
                        ;

var_list                ::= decl_list:l {: RESULT = l; :}
                          | {: RESULT = null; :}
                        ; 

proc_decl               ::= PROCEDURE proc_decl_type:t ID:n
                            LPAR param_list:pl RPAR
                            LBRACK proc_decl_decls:pd stmts:st RBRACK
                            
                            {: RESULT = new ProcDecl(n, t, pl, pd, st); :}
                        ; 

proc_decl_decls         ::= decl_list:dl {:RESULT = dl; :}
                            | {:RESULT = null; :}
                        ;

proc_decl_type          ::= RET type:t {: RESULT = t; :}
                            | {: RESULT = null; :}
                        ;
                        
class_decl		          ::= CLASS ID:name LBRACK var_list:v RBRACK
                          {: RESULT = new ClassDecl(name, v); :}
				                ;

param_decl              ::= param_ref:ref type:t ID:name {: RESULT = new ParamDecl(name, t, ref); :}
                            | {:RESULT = null; :}
                        ;
                        
param_ref               ::= REFERENCE {: RESULT = true; :}
                            | {:RESULT = false; :}
                        ;

param_list              ::= param_decl:p {: List<Decl> l = new LinkedList<Decl>();
                            l.add(p);
                            RESULT = l; :} 
                          | param_list:pl COMMA param_decl:p 
                            {: pl.add(p); RESULT = pl; :}
                        ;

/*exp			::= or_exp:oe {: RESULT = oe; :} 
                        ;
*/


or_exp			::= or_exp:e1 OR and_exp:e2 {: RESULT = new OrExp(e1, e2); :} 
			| and_exp:ae {: RESULT = ae; :}
//			| not_exp:ne {: RESULT = ne; :}
			;

and_exp			::= and_exp:e1 AND not_exp:e2 {: RESULT = new AndExp(e1, e2); :}
			| not_exp:ne {: RESULT = ne; :}
			;

not_exp			::= NOT not_exp:e1 {: RESULT = new NotExp(e1); :} 
    			| rel_exp:re {: RESULT = re; :}
			//| last_exp:ls {: RESULT = ls; :}			
			;


rel_exp			::= arit1_exp:e1 rel_op:ro arit1_exp:e2 
			{: RESULT = new RelExp(e1, e2, ro); :}
			| arit1_exp:ae {: RESULT = ae; :}
			;

arit1_exp		::= arit1_exp:e1 arit_op1:ao arit2_exp:e2 
			{: RESULT = new Arit1Exp(e1, e2, ao); :} 
			| arit2_exp:ae {: RESULT = ae; :};
			
arit2_exp		::= arit2_exp:e1 arit_op2:ao expo_exp:e2 
			{: RESULT = new Arit2Exp(e1, e2, ao); :} 
			| expo_exp:ee {: RESULT = ee; :};


expo_exp		::= last_exp:e1 EXPO expo_exp:e2 
			{: RESULT = new ExpoExp(e1, e2); :} 
			| last_exp:le {: RESULT = le; :}
			;


last_exp		::= LPAR or_exp:e RPAR {: RESULT = e; :} 
			| call_stmt:cs {: CallExp ce = cs.gexp(); RESULT = ce; :}
			| literal:l {: RESULT = new LastExp(l, "literal"); :}
			| NEW ID:name {: RESULT = new LastExp(name, "new"); :}
			| ID:name {: RESULT = new NameExp(name); :}
			| last_exp:le DOT ID:name {: RESULT = new NameExp(name, le); :}
			; 
			


log_op                  ::= AND         {: RESULT = "&&"; :} 
                            | OR        {: RESULT = "||"; :}
                        ;
                        
rel_op                  ::= LCHEV       {: RESULT = "<"; :}
                            | LCHEVEQ   {: RESULT = "<="; :}
                            | RCHEV     {: RESULT = ">"; :}
                            | RCHEVEQ   {: RESULT = ">="; :}
                            | EQUALS    {: RESULT = "="; :}
                            | NEQUAL    {: RESULT = "<>"; :}
                        ;
                        
arit_op1                 ::= PLUS        {: RESULT = "+"; :}
                             | MINUS     {: RESULT = "-"; :}
                        ;

arit_op2		::= TIMES     {: RESULT = "*"; :}
                            | DIVIDE    {: RESULT = "/"; :}
			    ;

literal                 ::= FLOAT_LITERAL:f     {: RESULT = ""+f; :} 
                            | INT_LITERAL:i     {: RESULT = ""+i; :}
                            | STRING_LITERAL:s  {: RESULT = s; :}
                            | TRUE              {: RESULT = "true"; :}
                            | FALSE             {: RESULT = "false"; :}
                            | NULL              {: RESULT = "null"; :}
                        ;


stmt                    ::= assign_stmt:as SEMI    {: RESULT = as; :}
                            | if_stmt:is           {: RESULT = is; :}
                            | while_stmt:ws        {: RESULT = ws; :}
                            | return_stmt:rs SEMI  {: RESULT = rs; :}
			    | call_stmt:cs SEMI    {: RESULT = cs; :}
			                  ;

                            
stmt_list               ::= stmt:s {: List<Stmt> l = new LinkedList<Stmt>();
                            l.add(s);
                            RESULT = l; :} 
                          | stmt_list:sl stmt:s 
                            {: sl.add(s); RESULT = sl; :}
                        ;

stmts                   ::= stmt_list:sl {: RESULT = sl; :}
                            | {: RESULT = null; :}
                        ;

assign_stmt             ::= or_exp:e1 ASSIGNMENT or_exp:e 
                            {: RESULT = new AssignStmt(e1, e); :} 
			                  ;

if_stmt                 ::= IF or_exp:ex THEN LBRACK stmts:st RBRACK if_else:el
                            {: RESULT = new IfStmt(ex, st, el); :}
                        ;

if_else                 ::= ELSE LBRACK stmts:st RBRACK {: RESULT = st; :}
                            | {: RESULT = null; :}
                        ;

while_stmt              ::= WHILE or_exp:ex DO LBRACK stmts:st RBRACK
                            {: RESULT = new WhileStmt(ex, st); :}
                        ;

return_stmt             ::= RETURN or_exp:e {:RESULT = new ReturnStmt(e); :}
                            | RETURN {: RESULT = new ReturnStmt(null); :}
			                  ;

call_stmt               ::= ID:name LPAR call_params:cp RPAR
                            {: RESULT = new CallStmt(name, cp); :}
                        ;
                        
call_params             ::= actual_param:p {: List<Exp> l = new LinkedList<Exp>();
                            if (p != null) l.add(p);
                            RESULT = l; :} 
                          | call_params:cp COMMA actual_param:p 
                            {: if (p != null) cp.add(p); RESULT = cp; :}
                        ;

actual_param            ::= REFERENCE or_exp:v   {: RESULT = v; :}
                            | or_exp:e           {: RESULT = e; :}
                            |                 {: RESULT = null; :}

                        ;

type                    ::= FLOAT     {: RESULT = "float";  :}
                            | INT     {: RESULT = "int";    :}
                            | STRING  {: RESULT = "string"; :} 
                            | BOOL    {: RESULT = "bool";   :}
                            | ID:name {: RESULT = name;     :}
                        ;

